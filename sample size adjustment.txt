
/*Non-Inferiority margin adjustment*/
data NI_ADJ;
	NI_ADJ_TP=(0.3*0.9) + (0.3*(1-0.9)*(1-0.50));
	NI_ADJ_TP_HYP=(0.3*0.9) + (0.3*(1-.95)*(1-0.50)) + (0.3*(1-0.95)*(1-0.75));
run;



/*Calculate the sample size*/
proc power;
	twosamplemeans
	sides = l
	groupweights = (1  1)
	MEANDIFF = 0 
	/* Adjsuted Ni_Marigin*/
	nulldiff = 0.3 0.285 0.28125
	stddev = 1.1
	power =  .76 .78 .80 .82 .84 .86
	alpha = 0.025
	ntotal = .
	TEST=diff;
	ods output output = power;
;
run;

/*Name the method used*/
data power1;
	set power;
	if nulldiff in  (.3) then method="exact";
    if nulldiff in  (.285) then method="TP";
	if nulldiff in  (.28125) then method="HYPO";
run;

/*Sample size adjustment - traditional*/
data power2;
	set power1;
	if nulldiff in (.3);
	ntotal=round(ntotal + (ntotal*20/100),8.);
	Method="Adj";
run;

data power3;
	set power1 power2;
run;


/*To make the table*/
proc sort data=power3; by nominalpower; run;
proc transpose data=power3 out=Power_wide (drop= _name_ _label_);
  by  nominalpower;
  id  method;
  var ntotal;
run; 


/*plotting*/
proc sgplot data=power3;
    series 	x=ntotal y=nominalpower / group=method;
	refline .8;
run;

